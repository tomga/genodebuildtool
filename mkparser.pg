


CmdList: Command | CmdList LINE_END Command;

Command: VAR_IDENT CmdAssign |
         ExportWithWs VAR_IDENT CmdAssign? |
				 CondCmd |
				 IncludeCmd |
				 VpathCmd |
				 RValueExpr |
				 WSOPT |
				 COMMENT;

CmdAssign: WSOPT CmdOper WSOPT OptRValueExpr;

ExportWithWs: EXPORT WS;

CmdOper: CMD_APPEND |
         CMD_RECURSIVE_EXPAND_ASSIGN |
				 CMD_SIMPLE_EXPAND_ASSIGN |
				 CMD_OPTASSIGN;

CondCmd: Condition LINE_END CmdList LINE_END "endif" |
         Condition LINE_END CmdList LINE_END "else" LINE_END CmdList LINE_END "endif" |
         Condition LINE_END CmdList LINE_END "else" WS CondCmd;

Condition: IfdefOper WS VarName |
           IfeqOper WS BRACE_OPEN ParamExpr CommaSeparator ParamExpr BRACE_CLOSE;

IfdefOper: "ifdef" | "ifndef";

IfeqOper: "ifeq" | "ifneq";

IncludeCmd: "include" WSOPT OptRValueExpr |
						"-include" WSOPT OptRValueExpr;

VpathCmd: "vpath" WS RValueExpr WS RValueExpr;

WSOPT: WS | EMPTY;

OptRValueExpr: RValueExpr |
							 EMPTY;

RValueExpr: RValuePart |
            RValueExpr RValuePart |
            RValueExpr WS;

RValuePart: RValuePartText |
						RValuePartExpr;


RValuePartExpr: DOLLAR VarName |
            		DOLLAR BRACE_OPEN Fun1Name WS ParamExpr BRACE_CLOSE |
            		DOLLAR BRACE_OPEN Fun2Name WS ParamExpr CommaSeparator ParamExpr BRACE_CLOSE |
            		DOLLAR BRACE_OPEN Fun3Name WS ParamExpr CommaSeparator ParamExpr CommaSeparator ParamExpr BRACE_CLOSE |
            		DOLLAR BRACE_OPEN FunAnyName WS ParamList BRACE_CLOSE |
            		DOLLAR BRACE_OPEN VarName BRACE_CLOSE |
            		DOLLAR BRACE_OPEN VAR_INDEX BRACE_CLOSE |
            		DOLLAR BRACE_OPEN VarName ":" RValueExpr CMD_RECURSIVE_EXPAND_ASSIGN RValueExpr BRACE_CLOSE;

ParamList: ParamExpr |
           ParamList CommaSeparator ParamExpr;

ParamExpr: ParamPart |
           ParamExpr ParamPart |
           ParamExpr WS |
					 EMPTY;

ParamPart: ParamPartText |
					 ParamPartExpr;


ParamPartExpr: DOLLAR VarName |
           		 DOLLAR BRACE_OPEN Fun1Name WS ParamExpr BRACE_CLOSE |
            	 DOLLAR BRACE_OPEN Fun2Name WS ParamExpr CommaSeparator ParamExpr BRACE_CLOSE |
            	 DOLLAR BRACE_OPEN Fun3Name WS ParamExpr CommaSeparator ParamExpr CommaSeparator ParamExpr BRACE_CLOSE |
            	 DOLLAR BRACE_OPEN FunAnyName WS ParamList BRACE_CLOSE |
            	 DOLLAR BRACE_OPEN VarName BRACE_CLOSE |
            	 DOLLAR BRACE_OPEN VAR_INDEX BRACE_CLOSE |
            	 DOLLAR BRACE_OPEN VarName ":" ParamExpr CMD_RECURSIVE_EXPAND_ASSIGN ParamExpr BRACE_CLOSE;

VarName: VAR_IDENT |
         VAR_IDENT RValuePartExpr;

RValuePartText: TEXT |
                COMMA |
								CMD_RECURSIVE_EXPAND_ASSIGN |
            		ESCAPED |
								LINE_CONTINUATION;

ParamPartText: TEXT |
							 CMD_RECURSIVE_EXPAND_ASSIGN |
            	 ESCAPED |
							 LINE_CONTINUATION;

CommaSeparator: COMMA WS?;

Fun1Name: "firstword" |
					"lastword" |
					"realpath" |
					"sort" |
					"wildcard";

Fun2Name: "addprefix" |
					"call" |
					"filter" |
					"filter-out" |
					"findstring";

Fun3Name: "foreach" |
					"if" |
					"subst";

FunAnyName: "error" |
            "shell";

terminals
LINE_END: /\n/;
TEXT: /[^#,$\\\n ()]/;
ESCAPED: /\\./;
LINE_CONTINUATION: /\\\n/;
WS: /[ \t]+/;
COMMENT: /#.*/;
VAR_IDENT: /[A-Za-z][A-Za-z0-9_]*|@|\*/;
VAR_INDEX: /[0-9]/;
DOLLAR: /\$/;
COMMA: /,/;
BRACE_OPEN: /\(/;
BRACE_CLOSE: /\)/;
CMD_APPEND: "+=";
CMD_RECURSIVE_EXPAND_ASSIGN: "=";
CMD_SIMPLE_EXPAND_ASSIGN: ":=";
CMD_OPTASSIGN: "?=";
EXPORT: "export";
