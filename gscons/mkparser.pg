
CmdList: Command |
         CmdList LINE_END Command;

Command: SafeValueExpr CmdAssign |
         EXPORT WS SafeValueExpr CmdAssign? |
         UNEXPORT WS SafeValueExpr |
         CondCmd |
         IncludeCmd |
         VPathCmd |
         RuleCmd |
         WSOPT |
         COMMENT;


CmdAssign: CmdOper OptValueExpr;

CmdOper: CMD_APPEND |
         CMD_RECURSIVE_EXPAND_ASSIGN |
         CMD_SIMPLE_EXPAND_ASSIGN |
         CMD_OPTASSIGN;


CondCmd: Condition LINE_END CmdList LINE_END "endif" |
         Condition LINE_END CmdList LINE_END "else" LINE_END CmdList LINE_END "endif" |
         Condition LINE_END CmdList LINE_END "else" WS CondCmd;

Condition: IfdefOper WS ValueExpr |
           IfeqOper WS BRACE_OPEN NoCommaOptValueExpr COMMA NoCommaOptValueExpr BRACE_CLOSE;

IfdefOper: "ifdef" | "ifndef";

IfeqOper: "ifeq" | "ifneq";


IncludeCmd: "include" WS ValueExpr |
            "-include" WS ValueExpr;


VPathCmd: "vpath" WS VPATH_PATTERN WS ValueExpr;


RuleCmd: SafeValueExpr COLON OptValueExpr |
         RuleCmd LINE_END_TAB OptValueExpr;

WSOPT: WS | EMPTY;

OptValueExpr: ValueExpr |
              EMPTY;

ValueExpr: NoCommaValueExpr |
           COMMA |
           ValueExpr NoCommaValueExpr |
           ValueExpr COMMA |
           ValueExpr BRACE_OPEN ValueExpr BRACE_CLOSE;

NoCommaOptValueExpr: NoCommaValueExpr |
                     EMPTY;

NoCommaValueExpr: SafeValueExpr |
                  CMD_RECURSIVE_EXPAND_ASSIGN |
                  COLON |
                  NoCommaValueExpr SafeValueExpr |
                  NoCommaValueExpr CMD_RECURSIVE_EXPAND_ASSIGN |
                  NoCommaValueExpr COLON |
                  NoCommaValueExpr BRACE_OPEN ValueExpr BRACE_CLOSE;

SafeValueExpr: SafeValuePart |
               SafeValueExpr SafeValuePart |
               SafeValueExpr BRACE_OPEN ValueExpr BRACE_CLOSE;

SafeValuePart: SAFE_RVALUE_CHAR |
               LINE_CONTINUATION;

LINE_END: LINE_END_ONLY |
          LINE_END_TAB;


terminals
LINE_END_TAB: /\n\t/;
LINE_END_ONLY: /\n/;
SAFE_RVALUE_CHAR: /[^(){}#\n:=,]/;
BRACE_OPEN:  /[({]/;
BRACE_CLOSE: /[)}]/;
LINE_CONTINUATION: /\\\n/;
WS: /[ ]+/;
COMMENT: /\#.*/;
COMMA: ",";
COLON: ":";
CMD_APPEND: /\+=/;
CMD_RECURSIVE_EXPAND_ASSIGN: "=";
CMD_SIMPLE_EXPAND_ASSIGN: ":=";
CMD_OPTASSIGN: "?=";
UNEXPORT: "unexport";
EXPORT: "export";
VPATH_PATTERN: /[a-zA-Z_0-9.%\/][a-zA-Z_0-9.%\/]*/;